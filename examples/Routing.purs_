module Example.Routing where
import Prelude hiding (div)
import Effect (Effect)
import Data.String (stripPrefix, Pattern(..))
import Data.Int (fromString)
import Data.Maybe (fromMaybe)
import Pha.App (Document, attachTo)
import Pha.App.Router (appWithRouter, Url, UrlRequest(..))
import Pha.Update (Update, get, put)
import Pha ((/\))
import Pha as H
import Pha.Elements as HH
import Pha.Attributes as P
import Pha.Events as E
import Pha.Effects.Nav (NAV, interpretNav)
import Pha.Effects.Nav as Nav
import Run as Run

type State = Url

data Msg = OnUrlChange Url | OnUrlRequest UrlRequest | NextPage

-- effects used in this app
type EFFS = (nav ∷ NAV)

-- |  "/page32" → 32
extractNumber ∷ String → Int
extractNumber = (stripPrefix (Pattern "/page") >=> fromString) >>> fromMaybe 0

update ∷ Msg → Update State EFFS
update (OnUrlChange url) = put url
update (OnUrlRequest (Internal url)) = Nav.goTo url.href
update (OnUrlRequest (External url)) = Nav.load url
update NextPage = do
    {pathname} <- get
    Nav.goTo $ "/page" <> show (extractNumber pathname + 1)

view ∷ State → Document Msg
view {href, pathname} = {
    title: "Routing example",
    body:
        HH.div [] 
        [   HH.h1 [] [H.text $ "Page: " <> href]
        ,   HH.a
                [P.href $ "/page" <> show (extractNumber pathname - 1)]
                [H.text "Previous page"]
        ,   HH.button
                [E.onclick NextPage]
                [H.text "Next page"]
        ]
}

main ∷ Effect Unit
main = appWithRouter 
    {   init: (_ /\ Nav.redirectTo "/page0")
    ,   view
    ,   update
    ,   subscriptions: const []
    ,   onUrlChange: OnUrlChange
    ,   onUrlRequest: OnUrlRequest
    ,   interpreter: Run.match {
            nav: interpretNav
        }
    } # attachTo "root"
